buildscript {
	ext.kotlin_version = "1.1.1"
	repositories {
		mavenCentral()
		maven {
			url "https://oss.sonatype.org/content/repositories/snapshots/"
		}
		maven {
			url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
		}
	}
}

plugins {
	id "org.jetbrains.intellij" version "0.2.16"
	id "org.jetbrains.kotlin.jvm" version "1.1.1"
}
apply plugin:"java"
apply plugin:"kotlin"
apply plugin:"idea"
apply plugin:"org.jetbrains.intellij"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "lib" }
}
enableProvidedScope()

dependencies {
	// dependencies below are "provided" because they should be on IDE classpath
	provided "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	provided "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
	provided "junit:junit:4.11" // also includes hamcrest-core
}

sourceSets {
	main {
		kotlin { srcDir "./src" }
		resources { srcDir "./src/resources" }
	}
	test {
		kotlin { srcDir "./test" }
	}
}

compileKotlin {
	kotlinOptions {
		jvmTarget = "1.8"
		apiVersion = "1.0"
		languageVersion = "1.0"
	}
}

def enableProvidedScope() {
	configurations {
		provided
		compile.extendsFrom provided
	}
	idea {
		module { scopes.PROVIDED.plus += [configurations.provided] }
	}
}
intellij {
	version "IU-172.3757.29"
	type "UI"
	pluginName "ijkl-shortcuts-plugin"
	downloadSources true
	sameSinceUntilBuild false
	updateSinceUntilBuild false
}